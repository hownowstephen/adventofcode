HAI 1.2
    VISIBLE "HAI, DECEMBER 12!!2!"

    I HAS A WHERAM ITZ A BUKKIT
    WHERAM HAS A POYNTX ITZ 10
    WHERAM HAS A POYNTY ITZ -1
    WHERAM HAS A BOATX ITZ 0
    WHERAM HAS A BOATY ITZ 0

    HOW IZ I ABS YR VALU
        FOUND YR BIGGR OF VALU AN PRODUKT OF VALU AN -1
    IF U SAY SO

    HOW IZ I IMTERPRET YR POSISHUN AN YR DIRECSHUN AN YR BIGNESS
        DIRECSHUN, WTF?
            OMG "N"
                BIGNESS R PRODUKT OF BIGNESS AN -1
                POSISHUN'Z POYNTY R SUM OF POSISHUN'Z POYNTY AN BIGNESS
            GTFO
            OMG "S"
                POSISHUN'Z POYNTY R SUM OF POSISHUN'Z POYNTY AN BIGNESS
            GTFO
            OMG "W"
                BIGNESS R PRODUKT OF BIGNESS AN -1
                POSISHUN'Z POYNTX R SUM OF POSISHUN'Z POYNTX AN BIGNESS
            GTFO
            OMG "E"
                POSISHUN'Z POYNTX R SUM OF POSISHUN'Z POYNTX AN BIGNESS
            GTFO
            OMG "F"
                POSISHUN'Z BOATX R SUM OF POSISHUN'Z BOATX AN PRODUKT OF BIGNESS AN POSISHUN'Z POYNTX
                POSISHUN'Z BOATY R SUM OF POSISHUN'Z BOATY AN PRODUKT OF BIGNESS AN POSISHUN'Z POYNTY
            GTFO
            OMG "R"
                IM IN YR GONRITE UPPIN YR COUNTIN TIL BOTH SAEM COUNTIN AN QUOSHUNT OF BIGNESS AN 90
                    I HAS A NEWX ITZ PRODUKT OF POSISHUN'Z POYNTY AN -1
                    POSISHUN'Z POYNTY R POSISHUN'Z POYNTX
                    POSISHUN'Z POYNTX R NEWX
                IM OUTTA YR GONRITE
            GTFO
            OMG "L"
                IM IN YR GONLEFT UPPIN YR COUNTIN TIL BOTH SAEM COUNTIN AN QUOSHUNT OF BIGNESS AN 90
                    I HAS A NEWY ITZ PRODUKT OF POSISHUN'Z POYNTX AN -1
                    POSISHUN'Z POYNTX R POSISHUN'Z POYNTY
                    POSISHUN'Z POYNTY R NEWY
                IM OUTTA YR GONLEFT
            GTFO
            OMGWTF
                VISIBLE "DUNNO LOL " DIRECSHUN
            GTFO
        OIC
        FOUND YR POSISHUN
    IF U SAY SO

    I HAS A IZDUN ITZ "FAIL"
    I HAS A IMDEX ITZ 0 BTW this isn't useful, but required to have a TIL in the loop
    IM IN YR READR UPPIN YR IMDEX TIL BOTH SAEM IZDUN AN "WIN"
        I HAS A DIRECSHUN ITZ A YARN
        GIMMEH DIRECSHUN
        BOTH SAEM DIRECSHUN AN "", O RLY?
        YA RLY
            IZDUN R "WIN"
        NO WAI
            I HAS A BIGNESS ITZ A NUMBAR
            GIMMEH BIGNESS
            WHERAM R I IZ IMTERPRET YR WHERAM AN YR DIRECSHUN AN YR BIGNESS MKAY
        OIC
    IM OUTTA YR READR

    BTW VISIBLE WHERAM'Z HORZNL "," WHERAM'Z VERTCL " POINT AT " WHERAM'Z  POINTING
    VISIBLE SUM OF I IZ ABS YR WHERAM'Z BOATX MKAY AN I IZ ABS YR WHERAM'Z BOATY MKAY

KTHXBYE