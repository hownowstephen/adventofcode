HAI 1.2
    VISIBLE "HAI, DECEMBER 12!!1!"

    I HAS A WHERAM ITZ A BUKKIT
    WHERAM HAS A HORZNL ITZ 0
    WHERAM HAS A VERTCL ITZ 0
    WHERAM HAS A POINTING ITZ 0

    HOW IZ I ABS YR VALU
        FOUND YR BIGGR OF VALU AN PRODUKT OF VALU AN -1
    IF U SAY SO

    HOW IZ I IMTERPRET YR POSISHUN AN YR DIRECSHUN AN YR BIGNESS
        BTW VISIBLE "GON " DIRECSHUN " BY " BIGNESS
        DIRECSHUN, WTF?
            OMG "N"
                BIGNESS R PRODUKT OF BIGNESS AN -1
                POSISHUN'Z VERTCL R SUM OF POSISHUN'Z VERTCL AN BIGNESS
            GTFO
            OMG "S"
                POSISHUN'Z VERTCL R SUM OF POSISHUN'Z VERTCL AN BIGNESS
            GTFO
            OMG "W"
                BIGNESS R PRODUKT OF BIGNESS AN -1
                POSISHUN'Z HORZNL R SUM OF POSISHUN'Z HORZNL AN BIGNESS
            GTFO
            OMG "E"
                POSISHUN'Z HORZNL R SUM OF POSISHUN'Z HORZNL AN BIGNESS
            GTFO
            OMG "F"
                I HAS A WHERPOINT ITZ A YARN
                POSISHUN'Z POINTING, WTF?
                    OMG 0
                        WHERPOINT R "E"
                    GTFO
                    OMG 1
                        WHERPOINT R "S"
                    GTFO
                    OMG 2
                        WHERPOINT R "W"
                    GTFO
                    OMG 3
                        WHERPOINT R "N"
                    GTFO
                    OMGWTF
                        VISIBLE "WAT " POSISHUN'Z POINTING
                    GTFO
                OIC
                FOUND YR I IZ IMTERPRET YR POSISHUN AN YR WHERPOINT AN YR BIGNESS MKAY
            GTFO
            OMG "R"
                POSISHUN'Z POINTING R MOD OF SUM OF POSISHUN'Z POINTING AN QUOSHUNT OF BIGNESS AN 90 AN 4
            GTFO
            OMG "L"
                I HAS A NEWPOSISHUN ITZ DIFF OF POSISHUN'Z POINTING AN QUOSHUNT OF BIGNESS AN 90
                BOTH SAEM NEWPOSISHUN AN I IZ ABS YR NEWPOSISHUN MKAY, O RLY?
                YA RLY
                    POSISHUN'Z POINTING R NEWPOSISHUN
                NO WAI
                    POSISHUN'Z POINTING R SUM OF NEWPOSISHUN AN 4
                OIC
            GTFO
            OMGWTF
                VISIBLE "DUNNO LOL " DIRECSHUN
            GTFO
        OIC
        FOUND YR POSISHUN
    IF U SAY SO

    I HAS A IZDUN ITZ "FAIL"
    I HAS A IMDEX ITZ 0 BTW this isn't useful, but required to have a TIL in the loop
    IM IN YR READR UPPIN YR IMDEX TIL BOTH SAEM IZDUN AN "WIN"
        I HAS A DIRECSHUN ITZ A YARN
        GIMMEH DIRECSHUN
        BOTH SAEM DIRECSHUN AN "", O RLY?
        YA RLY
            IZDUN R "WIN"
        NO WAI
            I HAS A BIGNESS ITZ A NUMBAR
            GIMMEH BIGNESS
            WHERAM R I IZ IMTERPRET YR WHERAM AN YR DIRECSHUN AN YR BIGNESS MKAY
        OIC
    IM OUTTA YR READR

    BTW VISIBLE WHERAM'Z HORZNL "," WHERAM'Z VERTCL " POINT AT " WHERAM'Z  POINTING
    VISIBLE SUM OF I IZ ABS YR WHERAM'Z HORZNL MKAY AN I IZ ABS YR WHERAM'Z VERTCL MKAY

KTHXBYE